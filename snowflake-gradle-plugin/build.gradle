plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id("io.freefair.lombok") version "8.0.1"
}

// Update release script to copy group and version from parent build script
group = 'com.snowflake'
version = "0.1.0"
description = 'Snowflake Gradle Plugin'
java.sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

dependencies {
    api 'net.snowflake:snowflake-jdbc:3.13.27'
    api 'commons-io:commons-io:2.11.0'
    testImplementation 'junit:junit:4.12'
    implementation project(':snowflake-plugins-core')
}

testing {
    suites {
        functionalTest(JvmTestSuite) {
            useJUnit('4.12')

            dependencies {
                implementation 'junit:junit:4.12'
                // functionalTest test suite depends on the production code in tests
                implementation project(":snowflake-gradle-plugin")
            }
            targets {
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                        failFast true
                    }
                }
            }
        }
    }
}

gradlePlugin {
    plugins {
        snowflakeGradlePlugin {
            id = 'com.snowflake.snowflake-gradle-plugin'
            implementationClass = 'com.snowflake.plugins.udf.gradle.SnowflakePlugin'
        }
    }
    testSourceSets.add(sourceSets.functionalTest)
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = getGroup()
            artifactId = getName()
            version = getVersion()
            from(components.java)
        }
    }
    repositories {
        mavenLocal()
    }
}
